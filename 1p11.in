formulas(assumptions).

    %% Self explanation
    all x ( self_explanatory(x) <-> (all y ( conceived_through(x,y) -> y=x) ) ).

    %% Part of the definition of substance
    % Substances are conceived through themselves alone.
    all x (substance(x) -> self_explanatory(x)).

    %% Conception and causation
    all x all y ( conceived_through(x,y) <-> causes(y,x) ).

    % Definition of essential involvement
    all x all F (
        involves_essentially(x,F)
        <-> all e ( is_essence_of(e,x) -> involves(e,F) )
    ).

    %% Definition of causa sui
    all x all F (
        causes(x,x) <-> involves_essentially(x,existence)
    ).

    % We need either this or 'everything has a cause'
    %% Everything is conceived through something.
    %all x (exists y conceived_through(x,y)).

    %% Everything has a cause.
    all x (exists y causes(y,x)).

    %% Part of the definition of God
    substance(God).

    % Relationship between conceivability and necessity.
    all x all F ( -conceivably_has(x,F) <-> necessarily_lacks(x,F) ).

    % Reformulations of above.
    all x all F ( necessarily_lacks(x,F) <-> -possibly_has(x,F) ).
    all x all F ( conceivably_lacks(x,F) <-> -necessarily_has(x,F) ).

    % What can be conceived as not existing doesn't essentially involve
    % existence.
    all x all F (
        conceivably_lacks(x,existence) -> -involves_essentially(x,existence)
    ).

end_of_list.

formulas(goals).

    necessarily_has(God,existence).

end_of_list.

% vim: set filetype=prover9 :
